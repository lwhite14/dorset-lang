fn unary!(v)
{
    if v then
        0
    else
        1;
}

fn unary-(v)
{
    0-v;
}

fn binary> 10 (LHS RHS)
{
    RHS < LHS;
}

fn binary| 5 (LHS RHS)
{
    if LHS then
        1
    else if RHS then
        1
    else
        0;
}

fn binary& 6 (LHS RHS)
{
    if !LHS then
        0
    else
        !!RHS;
}

fn binary= 9 (LHS RHS)
{
    !(LHS < RHS | LHS > RHS);
}


fn binary: 1 (x y)
{ 
    y;
}

fn printdensity(d)
{
    if d > 8 then
        printf(" ", 0)
    else if d > 4 then
        printf(".", 0)
    else if d > 2 then
        printf("+", 0)
    else
        printf("*", 0);
}

fn mandelconverger(real imag iters creal cimag)
{
    if iters > 255 | (real*real + imag*imag > 4) then
        iters
    else
        mandelconverger(real*real - imag*imag + creal,
                        2*real*imag + cimag,
                        iters+1, creal, cimag);
}


fn mandelconverge(real imag)
{
    mandelconverger(real, imag, 0, real, imag);
}

fn mandelhelp(xmin xmax xstep   ymin ymax ystep){
    for y = ymin, y < ymax, ystep in (
        (for x = xmin, x < xmax, xstep in
            printdensity(mandelconverge(x,y)))
            : printf("\n", 0)
        );
}

fn mandel(realstart imagstart realmag imagmag)
{
    mandelhelp(realstart, realstart+realmag*78, realmag,
                imagstart, imagstart+imagmag*40, imagmag);
}

fn main()
{
    mandel(-3, -2, 0.1, 0.14);
}